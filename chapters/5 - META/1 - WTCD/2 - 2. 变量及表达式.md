# 2. 变量及表达式
## 2.1 概述
WTCD 允许定义并使用变量

## 2.2 值的类型
WTCD 有以下值类型：

- `number`：数字，支持正负和小数
- `boolean`：布尔值，只能是 `true` 或者 `false`
- `string`：字符串
- `null`：空值
- `action`： 表示一个需要可执行的操作（目前只支持 goto 某个 section）
- `choice`：表示一个选项（其中记录了这个选项的文字和被选择后执行的操作）
- `selection`： 表示一次选择（每个 selection 含有多个 choice）

## 2.3 变量
WTCD 允许定义定义和使用变量。每个变量都有固定的类型，其中可使用的有上述每一个除了 `null` 以外的类型。

### 2.3.1 定义变量
若要定义变量，则需要使用 `declare` 块。

```
declare <declaration>
```

`<declaration>` 可以是单个定义或是用方括号包裹的若干个定义。每个定义采用如下格式：

```
<变量类型> <变量名> [= <初始值>]
```

#### 2.3.1.1 单一定义
举个例子：

```
declare number a = 100
declare boolean b = false
```

这两个 `declaration` 分别定义了一个类型为 `number`，初始值为 `100` 的变量 `a`，和一个类型为 `boolean`，初始值为 `false` 的变量 `b`。

#### 2.3.1.2 多个定义
上述例子中的两个定义也可以合并成一个：

```
declare [
  number a = 100
  boolean b = false
]
```

#### 2.3.1.3 代码丑化（简称请不要这么做）
因为换行在 WTCD 中与空格的作用几乎一致，这个定义也可以写成：

```
declare [ number a = 100 boolean b = false ]
```

其中大多数空格甚至能被省略：

```
declare[number a=100boolean b=false]
```

Just because you can doesn't mean you should. 尽管换行和许多空格是可选的，但是为了可读性，强烈建议不要忽略它们。

#### 2.3.1.4 自动初始化
与此同时，类型为 `number`，`boolean`，`string` 的变量的初始化可以被省略。如果省略，它们将会被自动初始化为 `0`，`false`，和 `""`（空字符串）。

因此，如下两组定义效果是一致的。

```
declare [
  number a = 0
  boolean b = false
  string c = ""
]
```

```
declare [
  number a
  boolean b
  string c
]
```

### 2.3.2 变量的赋值
变量几乎可以在所有需要值的场合使用。这里先介绍如何改变变量的值。

给出如下定义：

```
declare number a
```

那么在需要赋值的时候就可以写：

```
a = 100
```

这个操作就会把 `a` 的值设置为 100。

当然，等号右侧也可以换成一个表达式：

```
a = a + 50
```

这个操作就会让 `a` 的值自增 50。不过这个操作可以用 `+=` 操作符代替：

```
a += 50
```

具体表达式有哪些会在 **2.4 表达式** 中介绍。

这里需要注意的是，WTCD 的解释器在运行时会检查赋值给某个变量的值是否和这个变量的类型相同。举个例子，如下代码就会在运行时报错：

```
declare number a
a = true // WTCDError: Variable "a" can only hold values of type number. Received boolean false.
```

### 2.3.3 变量的作用域
WTCD 使用[静态作用域](https://zh.wikipedia.org/wiki/%E4%BD%9C%E7%94%A8%E5%9F%9F)（[Lexical Scoping](https://en.wikipedia.org/wiki/Scope_(computer_science)#Lexical_scoping)）。每一个由大括号包裹的**表达式组**将组成一个新的作用域（表达式组将在 **2.4 ）。// TODO here!

每个定义的变量的作用域从定义位置开始一直延伸到当前作用域结束。最外层定义的变量是全局变量。

举个例子：

```wtcd
declare number outside = 10

{
  declare number inside = 20
  outside // 10
  inside // 20
}

outside // 10
inside // WTCDError: Cannot locate lexical scope for variable "inside".
```

```wtcd
someVariable // WTCDError: Cannot locate lexical scope for variable "someVariable".
declare number someVariable = 100
```

内层作用域定义的变量会暂时覆盖（shadow）外层作用域定义的同名变量：

```wtcd
declare number someVariable = 100
{
  declare boolean someVariable = false
  someVariable // false
}
someVariable // 100
```

在同一作用域内不能定义同名变量：

```wtcd
declare number hey = 200
declare number hey = 300 // WTCDError: Variable "hey" has already been declared within the same lexical scope.
```

## 2.4 表达式
表达式就是由能够表达一个值的式子。简单的表达式由变量，数字，或者操作符组成。而复杂的表达式可以是一个代码块。

### 2.4.1 常量表达式（Literal Expression）
常量表达式，就是表示常量的表达式。在 WTCD 中，常量表达式有 4 种：数字，布尔值，字符串，null。

数字，布尔值，和 null 都很好理解：

```wtcd
// 数字常量表达式
100
12.7  // 支持小数
.68   // 0. 开头的 0 可以省略
(-20) // 这个其实不是数字常量表达式，而是一个一元运算符表达式，但是为了方便理解，这里也列出来了

// 布尔值常量表达式
true
false

// null 常量表达式
null // 只能是 null
```

WTCD 的字符串常量表达式稍微复杂一些。WTCD 允许使用双引号（`"`），单引号（`'`），或者反引号（`` ` ``）来包裹字符串：

```wtcd
"Hello"
'World'
`Test`
```

WTCD 的字符串常量表达式支持使用反斜杠（`\`）进行 escape：

```wtcd
"\n" // -> 换行
"\t" // -> TAB
"\'" // -> 单引号
"\"" // -> 双引号
"\`" // -> 反引号
"\\" // -> 反斜杠
```

同时 WTCD 支持跨行字符串常量表达式：

```wtcd
"This is a
poem." // -> "This is a\npoem"
```

### 2.4.2 操作符表达式
WTCD 允许使用操作符来操作不同的值。这里先给几个例子：

```wtcd
1 + 1         // -> 2
1 + 2 * 3     // -> 7（乘法优先级更高）
(1 + 2) * 3   // -> 9（括号优先运算）
"a" + "b"     // -> "ab"（字符串拼接）
4 < 10        // -> true
5 >= 5        // -> true
3 == 3        // -> true
3 == "3"      // -> false（无自动类型转换）
3 != 3        // -> false
5 / 2         // -> 2.5（number 可存储小数）
5 // 2        // -> 2（truncate 整除）
-5 // 2       // -> -2（truncate 整除永远都往零靠）
5 & 2         // -> 1（求余）
-10           // -> -10
!false        // -> true
true ? 1 : 2  // -> 1（三元）
false && true // -> false
false || true // -> true
a = 3         // -> a 变成 3（变量赋值）
a += 5        // -> a 加上了3（变量累加）
```

完整的操作符请参见下表（并不需要全部记住）：

| 元数 | 操作符 | 优先级 | 作用                 | 例子                                                  |
| ---- | ------ | ------ | -------------------- | ----------------------------------------------------- |
| 1    | -      | 16     | 取负                 | `-42` = -42                                           |
| 1    | !      | 16     | 取逻辑反             | `!true` = false, `!false` = true                      |
| 2    | *      | 14     | 数字乘法             | `5 * 10` = 50                                         |
| 2    | /      | 14     | 数字除法             | `10 / 5` = 2, `5 / 2` = 2.5                           |
| 2    | //     | 14     | Truncate 整除        | `5 / 3` = 1, `-5 / 3` = -1                            |
| 2    | %      | 14     | 求余                 | `5 % 2` = 1, `-9 % 5` = -4                            |
| 2    | +      | 13     | 数字加法或字符串拼接 | `5 + 3` = 8, `"a" + "b"` = "ab"                       |
| 2    | -      | 13     | 数字减法             | `5 - 3` = 2, `3 - 5` = -2                             |
| 2    | <      | 11     | 判断是否小于         | `3 < 5` = true, `5 < 5` = false, `7 < 5` = false      |
| 2    | <=     | 11     | 判断是否小于或等于   | `3 <= 5` = true, `5 <= 5` = true, `7 <= 5` = false    |
| 2    | >      | 11     | 判断是否大于         | `3 > 5` = false, `5 > 5` = false, `7 > 5` = true      |
| 2    | >=     | 11     | 判断是否大于或等于   | `3 >= 5` = false, `5 >= 5` = true, `7 >= 5` = false   |
| 2    | ==     | 10     | 判断是否相等         | `5 == 5` = true, `5 == 3` = false, `"5" == 5` = false |
| 2    | !=     | 10     | 判断是否不相等       | `5 != 5` = false, `5 != 3` = true, `"5" != 5` = true  |
| 2    | &&     | 6      | 逻辑与               | `false || true` = false, `true || true` = true        |
| 2    | \|\|   | 5      | 逻辑或               | `false || true` = true, `false || false` = true       |
| 3    | ? :    | 4      | 逻辑选择             | `true ? 1 : 2` = 1, `false ? 1 : 2` = 2               |
| 2    | =      | 3      | 赋值                 | `a = 10` 把 10 赋值给 a，然后返回 10                  |
| 2    | +=     | 3      | 数字/字符串累加      | `a += 10` = `a = a + 10`, `b += "a"` = `b = b + "a"`  |
| 2    | -=     | 3      | 数字累减             | `a -= 10` = `a = a - 10`                              |
| 2    | *=     | 3      | 数字累乘             | `a *= 10` = `a = a * 10`                              |
| 2    | /=     | 3      | 数字累除             | `a /= 10` = `a = a / 10`                              |
| 2    | //=    | 3      | 数字累 Truncate 除   | `a //= 10` = `a = a // 10`                            |
| 2    | %=     | 3      | 数字累求余           | `a %= 10` = `a = a % 10`                              |


